*******************************************************
* Advanced Digital IC Design                          *
*                                                     *
* Demo3: UltraSIM Simulation Mode and Accuracy        *
*******************************************************

***********************************
* Operation Condition             *
***********************************
*** Process: Typical ***
*.lib "ultrasim_model.122" L18U18V_TT
*.lib "ultrasim_model.122" L18U18V_FF
.lib "ultrasim_model.122" L18U18V_SS

*** Voltage: 1.62V ***
.PARAM supply=1.62v

*** Temperature: 125C ***
.TEMP 125

***********************************
* Transition Analysis             *
***********************************
.TRAN 1ps 100ns

***********************************
* UltraSIM Simulation Options     *
***********************************
*ultrasim: .usim_opt  sim_mode=ms
*ultrasim: .usim_opt  postl=2 analog=0 speed=5

***********************************
* UltraSIM Output Options         *
***********************************
*ultrasim: .usim_opt  wf_format=fsdb 
*ultrasim: .usim_opt  wf_tres=1p
*ultrasim: .usim_opt  vh=0.9
*ultrasim: .usim_opt  vl=0.9

*ultrasim: .lprobe v(IN_clk) v(FB_clk) v(flAGU) v(flAGD)  v(QU) V(QD) V(outu) V(outd) v(outbu) v(outbd) v(QUBAR) 
+  v(QDBAR)  v(RESET_B) preserve=port

***********************************
* Output Analog Signals           *
***********************************
*.probe tran v(IN_clk) v(FB_clk) 

***********************************
* Measurement Commands            *
***********************************
*** Average && Peak Power ***
.MEAS tran avg_power avg par('v(VDD)*i(VDD)')
.MEAS tran max_power min par('v(VDD)*i(VDD)')

***********************************
* Define Global Nets              *
***********************************
.GLOBAL VDD VSS

***********************************
***********************************
* Voltage Sources                 *
***********************************
vdd     VDD   0  DC supply
vss     VSS   0  DC 0
*vinput   IN   0  PULSE(0 supply 0 0.1n 0.1n 49.9n 100n)
*vin      IN   0  PULSE(0 supply 0    0.1n 0.1n 49.9n 100n)
*vfb      FB   0  PULSE(0 supply 2.5n 0.1n 0.1n 49.9n 100n)
.VEC "dco1.vec"
*.VEC "30ps.vec"

***********************************
* TOP Circuit                     *
***********************************
* first DFFRX4 and NAND and AND
XG0  IN_clk  VDD  CDN QN1 QU  DFFRX4    
XG1  FB_clk  VDD  CDN QN2 QD  DFFRX4

XH0     QU      QD      QUQD   NAND2X1
XH1	Reset_B	QUQD	CDN	AND2X1
                       
* QU and QD inv
XG2  QU  QUBAR  INVX1 M=2
XG3  QD  QDBAR  INVX1 M=2

* OUTU and OUTD
XH2  	QU	QDBAR	OUTU NAND2X1 M=6
XH3  	QD	QUBAR	OUTD NAND2X1 M=6

*Digital Pulse Amplifier 
xAMP3  OUTU OUTBU AMP
xAMP4  OUTD OUTBD AMP

* second DFFRX4 FlagU and FlagD
XG6  IN_clk  VDD  OUTBU QN3 FLAGU  DFFRX4 
XG7  FB_clk  VDD  OUTBD QN4 FLAGD  DFFRX4 



***********************************
* Subckt Definition               *
***********************************
.SUBCKT AMP IN OUT
x0 IN   INBAR  BUFX3 

x1 IN   INBAR   IN1	AND2X1   
x2 INBAR  IN1   IN2	AND2X1
x3 IN2    IN1	IN3   	AND2X1
x4 IN3 	  IN2   IN4	AND2X1
x5 IN4    IN3	IN5   	AND2X1
x6 IN5    IN4   OUT	AND2X1

.ENDS 


.INCLUDE "ultrasim_cells.sp"

.END